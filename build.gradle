buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

def versions = [
        springBootVersion: '1.5.8.RELEASE'
]

allprojects {
    group 'uk.gov.hmcts.reform'
    version '0.3.0'

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'checkstyle'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'jacoco'


    checkstyle.toolVersion = '7.2'
    checkstyle.configFile = new File(rootDir, "checkstyle.xml")

    // make build fail on Checkstyle issues (https://github.com/gradle/gradle/issues/881)
    tasks.withType(Checkstyle).each { checkstyleTask ->
        checkstyleTask.doLast {
            reports.all { report ->
                def outputFile = report.destination
                if (outputFile.exists() && outputFile.text.contains("<error ")) {
                  throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
                }
            }
        }
    }

    sourceCompatibility = 1.8

    repositories {
        jcenter()
    }

    dependencies {
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBootVersion
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBootVersion

        compile group: 'com.google.guava', name: 'guava', version: '23.5-jre'

    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
    }
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'reform-api-standards'
            version project.version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'reform-api-standards'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/hmcts/reform-api-standards'
        version {
            name = project.version
        }
    }
}

dependencies {
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBootVersion
    testCompile group: 'io.rest-assured', name: 'rest-assured', version: '3.0.0'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'javax.inject', name: 'javax.inject', version: '1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
